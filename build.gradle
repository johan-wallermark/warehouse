plugins {
    id 'application'
    id 'com.google.cloud.tools.jib' version '2.8.0'
}

group 'org.example'
version '0.1.1'

application {
    mainClass = 'org.example.Warehouse'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
    }
}

sourceSets {
    integTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integTestImplementation.extendsFrom testImplementation
    integTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    // Javalin
    implementation 'io.javalin:javalin:3.13.4'
    implementation 'io.javalin:javalin-openapi:3.13.4'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.10.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.3'

    // Logging
    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'

    // DB
    implementation platform('org.jdbi:jdbi3-bom:3.18.1')
    implementation 'org.jdbi:jdbi3-core'
    implementation 'org.jdbi:jdbi3-postgres'
    implementation 'org.jdbi:jdbi3-sqlobject'
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'org.postgresql:postgresql:42.2.19.jre7'

    // Test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    integTestImplementation 'io.rest-assured:rest-assured:4.3.3'
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

check.dependsOn integrationTest

jib {
    from {
        image = 'adoptopenjdk:15-jre-hotspot'
    }
    to {
        image = 'demo/warehouse'
        tags = [version]
    }
}
