plugins {
    id 'application'
    id 'com.google.cloud.tools.jib' version '3.3.1'
}

group 'org.example'
version '0.1.1'

application {
    mainClass = 'org.example.Warehouse'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(19)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

sourceSets {
    integTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integTestImplementation.extendsFrom testImplementation
    integTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    // Javalin
    def openapi = "5.4.2"
    implementation 'io.javalin:javalin:5.4.2'
    annotationProcessor("io.javalin.community.openapi:openapi-annotation-processor:$openapi")
    implementation("io.javalin.community.openapi:javalin-openapi-plugin:$openapi")
    implementation("io.javalin.community.openapi:javalin-swagger-plugin:$openapi")
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'

    // Logging
    implementation platform('org.apache.logging.log4j:log4j-bom:2.20.0')
    implementation 'org.apache.logging.log4j:log4j-api'
    implementation 'org.apache.logging.log4j:log4j-core'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl'
    implementation 'org.slf4j:slf4j-simple:2.0.6'

    // DB
    implementation platform('org.jdbi:jdbi3-bom:3.37.1')
    implementation 'org.jdbi:jdbi3-core'
    implementation 'org.jdbi:jdbi3-postgres'
    implementation 'org.jdbi:jdbi3-sqlobject'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'org.postgresql:postgresql:42.5.4'

    // Test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    integTestImplementation 'io.rest-assured:rest-assured:5.3.0'
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

check.dependsOn integrationTest

jib {
    from {
        image = 'eclipse-temurin:19.0.2_7-jre-jammy'
    }
    to {
        image = 'demo/warehouse'
        tags = [version]
    }
}
